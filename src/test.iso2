type bool    = False | True
type 'a list = Nil (* [] *) | Cons (* :: *) of 'a * 'a list
type nat     = Z (* 0 *) | S (* n *) of nat
type q       = Q0 | Q1 | Q2 | Q3 | Q4 | Q5 | Q6 | Q7
type s       = B | S0 | S1 | S2 | S3 | S4 | S5 | S6 | S7

iso rec len = case
| [] <-> ([], 0)
| h :: t <->
  let (t, n) = len t in
  (h :: t, S n)
in

iso rec snoc' = case
| ([], x, 0) <->
  let (x1, x2) = (x, x) in
  ([x1], x2, 0)
| (h :: t, x, S n) <->
  let (t, x, n) = snoc' (t, x, n) in
  (h :: t, x, S n)
in

iso snoc = case (x, y) <->
  let (x, n) = len x in
  let (x, y, n) = snoc' (x, y, n) in
  let z = inv len (x, S n) in
  (z, y)
in

iso growth = case (l, r) <->
  let (l, B) = snoc (l, B) in
  let (r, B) = snoc (r, B) in
  (l, r)
in

iso rec it phi = case x <->
  let y = phi x in
  let z = match y with
    | (y, True)  <-> let (y, n) = it phi y in (y, S n)
    | (y, False) <-> (y, 0)
  in z
in

iso rec rm_blank = case
| []      <-> ([], 0)
| B :: t  <-> let (t, n) = rm_blank t in (t, S n)
| S0 :: t <-> (S0 :: t, 0)
| S1 :: t <-> (S1 :: t, 0)
| S2 :: t <-> (S2 :: t, 0)
| S3 :: t <-> (S3 :: t, 0)
| S4 :: t <-> (S4 :: t, 0)
| S5 :: t <-> (S5 :: t, 0)
| S6 :: t <-> (S6 :: t, 0)
| S7 :: t <-> (S7 :: t, 0)
in

iso rec rev_aux = case
| ([], y) <-> ([], y)
| (h :: t, y) <->
  let (h1, h2) = (h, h) in
  let (t1, t2) = rev_aux (t, h2 :: y) in
  (h1 :: t1, t2)
in

iso rev = case x <->
  let (t1, t2) = rev_aux (x, []) in
  (t1, t2)
in

iso clean_up = case ((x, (l, y, r)), n) <->
  let (l, n1) = rm_blank l in
  let (r_ori, r_rev) = rev r in
  let (r, n2) = rm_blank r_rev in
  ((x, (l, y, r)), n, n1, n2, r_ori)
in

iso garbrem f g = case x <->
  let (x, y) = f x in
  let (x, z) = (x, x) in
  let x = inv f (x, y) in
  let (z, y) = g z in
  let (z, z) = (z, x) in
  let z = inv g (z, y) in
  z
in

()
