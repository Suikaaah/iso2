type bool            = False | True
type 'a option       = None | Some of 'a
type ('l, 'r) either = Left of 'l | Right of 'r
type 'a list         = Nil (* [] *) | Cons (* :: *) of 'a * 'a list
type nat             = Z (* 0 *) | S (* n *) of nat
type ord             = Lt | Eq | Gt
type parity          = Even | Odd

iso rec parity = case
| 0       <-> (Even, 0)
| 1       <-> (Odd, 1)
| S (S x) <-> let (b, x) = parity x in (b, S (S x))
in

(* add (x, y) = (x + y, y) *)
iso rec add = case
| (m, 0)   <-> (m, 0)
| (m, S n) <-> let (m, n) = add (S m, n) in (m, S n)
in

iso rec cantor_pairing = case x <->
  let y = match x with
    | (S i, j) <-> Some (i, S j)
    | (0, S j) <-> Some (j, 0)
    | (0, 0)   <-> None
  in match y with
    | Some x <-> let y = cantor_pairing x in S y
    | None   <-> 0
in

iso rec cmp = case
| (0, S n)   <-> (Lt, 0, S n)
| (S n, 0)   <-> (Gt, S n, 0)
| (0, 0)     <-> (Eq, 0, 0)
| (S m, S n) <->
  let (o, m, n) = cmp (m, n) in
  (o, S m, S n)
in

iso rec fib = case
| 0 <-> (1, 1)
| S n <->
  let (x1, x2) = fib n in
  let (S (S x2'), x1) = add (x1, x2) in
  (x1, S (S x2'))
in

let (5, 8) = fib 4 in
let 4 = inv fib (5, 8) in

parity 37

